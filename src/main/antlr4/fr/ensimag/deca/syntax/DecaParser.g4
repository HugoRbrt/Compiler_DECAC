parser grammar DecaParser;

options {
    // Default language but name it anyway
    //
    language  = Java;

    // Use a superclass to implement all helper
    // methods, instance variables and overrides
    // of ANTLR default methods, such as error
    // handling.
    //
    superClass = AbstractDecaParser;

    // Use the vocabulary generated by the accompanying
    // lexer. Maven knows how to work out the relationship
    // between the lexer and parser and will build the
    // lexer before the parser. It will also rebuild the
    // parser if the lexer changes.
    //
    tokenVocab = DecaLexer;

}

// which packages should be imported?
@header {
    import fr.ensimag.deca.tree.*;
    import java.io.PrintStream;
    import java.math.*;
    import org.apache.log4j.Logger;
}

@members {
    @Override
    protected AbstractProgram parseProgram() {
        return prog().tree;
    }
}

prog returns[AbstractProgram tree]
    : list_classes main EOF {
            assert($list_classes.tree != null);
            assert($main.tree != null);
            $tree = new Program($list_classes.tree, $main.tree);
            setLocation($tree, $list_classes.start);
        }
    ;

main returns[AbstractMain tree]
    : /* epsilon */ {
            $tree = new EmptyMain();
        }
    | block {
            assert($block.decls != null);
            assert($block.insts != null);
            $tree = new Main($block.decls, $block.insts);
            setLocation($tree, $block.start);
        }
    ;

block returns[ListDeclVar decls, ListInst insts]
    : OBRACE list_decl list_inst CBRACE {
            assert($list_decl.tree != null);
            assert($list_inst.tree != null);
            $decls = $list_decl.tree;
            $insts = $list_inst.tree;
        }
    ;

list_decl returns[ListDeclVar tree]
@init   {
            $tree = new ListDeclVar();
        }
    : decl_var_set[$tree]*
    ;

decl_var_set[ListDeclVar l]
    : type list_decl_var[$l,$type.tree] SEMI
    ;

list_decl_var[ListDeclVar l, AbstractIdentifier t]
    : dv1=decl_var[$t] {
        $l.add($dv1.tree);
        } (COMMA dv2=decl_var[$t] {
            $l.add($dv2.tree);
        }
      )*
    ;

decl_var[AbstractIdentifier t] returns[AbstractDeclVar tree]
@init   {
            $tree = null;
        }
    : i=ident {
            setLocation($i.tree, $i.start);
        }
      (eq=EQUALS e=expr {
            $tree = new DeclVar($t, $i.tree, new Initialization($e.tree));
            setLocation($tree.getInit(), $eq);
            setLocation($tree, $i.start);
            LOG.trace($tree);
        }
      )? {
            if ($tree == null) {
                $tree = new DeclVar($t, $i.tree, new NoInitialization());
                setLocation($tree, $i.start);
                LOG.trace($tree);
            }
        }
    ;

list_inst returns[ListInst tree]
@init   {
            $tree = new ListInst();
        }
    : (inst {
            $tree.add($inst.tree);
        }
      )*
    ;

inst returns[AbstractInst tree]
    : e1=expr SEMI {
            assert($e1.tree != null);
            $tree = $e1.tree;
        }
    | SEMI {
            $tree = new NoOperation();
            setLocation($tree, $start);
        }
    | PRINT OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);
            $tree = new Print(false, $list_expr.tree);
            setLocation($tree, $start);
        }
    | PRINTLN OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);
            $tree = new Println(false, $list_expr.tree);
            setLocation($tree, $start);
        }
    | PRINTX OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);
            $tree = new Print(true, $list_expr.tree);
            setLocation($tree, $start);
        }
    | PRINTLNX OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);
            $tree = new Println(true, $list_expr.tree);
            setLocation($tree, $start);
        }
    | if_then_else {
            assert($if_then_else.tree != null);
            $tree = $if_then_else.tree;
            setLocation($tree, $start);
            LOG.trace($tree);
        }
    | WHILE OPARENT condition=expr CPARENT OBRACE body=list_inst CBRACE {
            assert($condition.tree != null);
            assert($body.tree != null);
            $tree = new While($condition.tree, $body.tree);
            setLocation($tree, $start);
            LOG.trace($tree);
        }
    | RETURN expr SEMI {
            assert($expr.tree != null);
            $tree = new Return($expr.tree);
            setLocation($tree, $start);
            LOG.trace($tree);
        }
    ;

if_then_else returns[IfThenElse tree]
@init   {
            $tree = null;
            ListInst temp = new ListInst();
            ListInst elseElif = temp;

        }
    : if1=IF OPARENT condition=expr CPARENT OBRACE li_if=list_inst CBRACE {
            LOG.trace($tree);
        }
      (ELSE elsif=IF OPARENT elsif_cond=expr CPARENT OBRACE elsif_li=list_inst CBRACE {
            elseElif = new ListInst();
            IfThenElse Elif = new IfThenElse($elsif_cond.tree, $elsif_li.tree, elseElif);
            temp.add(Elif);
            setLocation(Elif, $elsif);
            LOG.trace($tree);
        }
      )*
      (ELSE OBRACE li_else=list_inst CBRACE {
            for (AbstractInst i: $li_else.tree.getList()) {
                elseElif.add(i);
            }
        }
      )?
      {
      $tree = new IfThenElse($condition.tree, $li_if.tree, temp);
      setLocation($tree, $if1);
      }
    ;

list_expr returns[ListExpr tree]
@init   {
            $tree = new ListExpr();
        }
    : (e1=expr {
        $tree.add($e1.tree);
        }
    (COMMA e2=expr {
        $tree.add($e2.tree);
        }
       )* )?
    ;

expr returns[AbstractExpr tree]
    : assign_expr {
            assert($assign_expr.tree != null);
            $tree = $assign_expr.tree;
            LOG.trace($tree);
        }
    ;

assign_expr returns[AbstractExpr tree]
    : e=or_expr (
        /* condition: expression e must be a "LVALUE" */ {
            if (! ($e.tree instanceof AbstractLValue)) {
                throw new InvalidLValue(this, $ctx);
            }
        }
        eq=EQUALS e2=assign_expr {
            assert($e.tree != null);
            assert($e2.tree != null);
            $tree = new Assign((AbstractLValue)$e.tree, $e2.tree);
            setLocation($tree, $eq);
            LOG.trace($tree);
        }
      | /* epsilon */ {
            assert($e.tree != null);
            $tree = $e.tree;
            LOG.trace($tree);
        }
      )
    ;

or_expr returns[AbstractExpr tree]
    : e=and_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            LOG.trace($tree);
        }
    | e1=or_expr op=OR e2=and_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Or($e1.tree, $e2.tree);
            setLocation($tree, $op);
            LOG.trace($tree);
       }
    ;

and_expr returns[AbstractExpr tree]
    : e=eq_neq_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            LOG.trace($tree);
        }
    |  e1=and_expr op=AND e2=eq_neq_expr {
            assert($e1.tree != null);                         
            assert($e2.tree != null);
            $tree = new And($e1.tree, $e2.tree);
            setLocation($tree, $op);
            LOG.trace($tree);
        }
    ;

eq_neq_expr returns[AbstractExpr tree]
    : e=inequality_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            LOG.trace($tree);
        }
    | e1=eq_neq_expr op=EQEQ e2=inequality_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Equals($e1.tree, $e2.tree);
            setLocation($tree, $op);
            LOG.trace($tree);
        }
    | e1=eq_neq_expr op=NEQ e2=inequality_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new NotEquals($e1.tree, $e2.tree);
            setLocation($tree, $op);
            LOG.trace($tree);
        }
    ;

inequality_expr returns[AbstractExpr tree]
    : e=sum_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            LOG.trace($tree);
        }
    | e1=inequality_expr op=LEQ e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new LowerOrEqual($e1.tree, $e2.tree);
            setLocation($tree, $op);
            LOG.trace($tree);
        }
    | e1=inequality_expr op=GEQ e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new GreaterOrEqual($e1.tree, $e2.tree);
            setLocation($tree, $op);
            LOG.trace($tree);
        }
    | e1=inequality_expr op=GT e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Greater($e1.tree, $e2.tree);
            setLocation($tree, $op);
            LOG.trace($tree);
        }
    | e1=inequality_expr op=LT e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Lower($e1.tree, $e2.tree);
            setLocation($tree, $op);
            LOG.trace($tree);
        }
    | e1=inequality_expr op=INSTANCEOF type {
            assert($e1.tree != null);
            assert($type.tree != null);
            $tree = new InstanceOf($e1.tree, $type.tree);
            setLocation($tree, $op);
        }
    ;


sum_expr returns[AbstractExpr tree]
    : e=mult_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            LOG.trace($tree);
        }
    | e1=sum_expr op=PLUS e2=mult_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Plus($e1.tree, $e2.tree);
            setLocation($tree, $op);
            LOG.trace($tree);
        }
    | e1=sum_expr op=MINUS e2=mult_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Minus($e1.tree, $e2.tree);
            setLocation($tree, $op);
            LOG.trace($tree);
        }
    ;

mult_expr returns[AbstractExpr tree]
    : e=unary_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            LOG.trace($tree);
        }
    | e1=mult_expr op=TIMES e2=unary_expr {
            assert($e1.tree != null);                                         
            assert($e2.tree != null);
            $tree = new Multiply($e1.tree, $e2.tree);
            setLocation($tree, $op);
            LOG.trace($tree);
        }
    | e1=mult_expr op=SLASH e2=unary_expr {
            assert($e1.tree != null);                                         
            assert($e2.tree != null);
            $tree = new Divide($e1.tree, $e2.tree);
            setLocation($tree, $op);
            LOG.trace($tree);
        }
    | e1=mult_expr op=PERCENT e2=unary_expr {
            assert($e1.tree != null);                                                                          
            assert($e2.tree != null);
            $tree = new Modulo($e1.tree, $e2.tree);
            setLocation($tree, $op);
            LOG.trace($tree);
        }
    ;

unary_expr returns[AbstractExpr tree]
    : MINUS e=unary_expr {
            assert($e.tree != null);
            $tree = new UnaryMinus($e.tree);
            setLocation($tree, $start);
            LOG.trace($tree);
        }
    | EXCLAM e=unary_expr {
            assert($e.tree != null);
            $tree = new Not($e.tree);
            setLocation($tree, $start);
            LOG.trace($tree);
        }
    | select_expr {
            assert($select_expr.tree != null);
            $tree = $select_expr.tree;
            LOG.trace($tree);
        }
    ;

select_expr returns[AbstractExpr tree]
    : e=primary_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            LOG.trace($tree);
        }
    | e1=select_expr d=DOT i=ident {
            assert($e1.tree != null);
            assert($i.tree != null);
            $tree = new Selection($e1.tree,$i.tree);
            setLocation($tree, $d);
            LOG.trace($tree);
        }
        (o=OPARENT args=list_expr CPARENT {
            // we matched "e1.i(args)"
            assert($args.tree != null);
            $tree = new MethodCall($e1.tree, $i.tree, $args.tree);
            setLocation($tree, $o);
            LOG.trace($tree);
        }
        | /* epsilon */ {
            // we matched "e.i"
        }
        )
    ;

primary_expr returns[AbstractExpr tree]
    : ident {
            assert($ident.tree != null);
            $tree = $ident.tree;
        }
    | m=ident o=OPARENT args=list_expr CPARENT {
            assert($args.tree != null);
            assert($m.tree != null);
            AbstractExpr th = new This(true);
            setLocation(th, $m.start);
            $tree = new MethodCall(th, $ident.tree, $args.tree);
            setLocation($tree, $o);
            LOG.trace($tree);
        }
    | OPARENT expr CPARENT {
            assert($expr.tree != null);
            $tree = $expr.tree;
            LOG.trace($tree);
        }
    | READINT OPARENT CPARENT {
            $tree = new ReadInt();
            setLocation($tree, $start);
            LOG.trace($tree);
        }
    | READFLOAT OPARENT CPARENT {
            $tree = new ReadFloat();
            setLocation($tree, $start);
            LOG.trace($tree);
        }
    | n=NEW ident OPARENT CPARENT {
            assert($ident.tree != null);
            $tree = new New($ident.tree);
            setLocation($tree, $n);
        }
    | cast=OPARENT type CPARENT OPARENT expr CPARENT {
            assert($type.tree != null);
            assert($expr.tree != null);
            $tree = new Cast($type.tree,$expr.tree);
            setLocation($tree,$cast);
        }
    | literal {
            assert($literal.tree != null);
            $tree = $literal.tree;
            setLocation($tree, $literal.start);
            LOG.trace($tree);
        }
    ;

type returns[AbstractIdentifier tree]
    : ident {
            assert($ident.tree != null);
            $tree = $ident.tree;
            LOG.trace($tree);
        }
    ;

literal returns[AbstractExpr tree]
@init   {
            // to treat exceptions with the right location
            $tree = new IntLiteral(-1);
            setLocation($tree, $start);
            Location tmploc = $tree.getLocation();
        }
    : INT {
        try {
            $tree = new IntLiteral(Integer.parseInt($INT.text));
        } catch (NumberFormatException e) {
            System.err.println(tmploc.getFilename() + ":" + tmploc.getLine()
                + ":" + tmploc.getPositionInLine() + ": int literal out of expected range.");
            throw e;
        }
        LOG.trace($tree);
        }
    | fd=FLOAT {
        char[] charArray = $fd.text.toCharArray();
        boolean nonZero = false;
        for (char c: charArray) {
            if (c != '0' && c != '.' && c != 'e' && c != '+'
                    && c != '-' && c != 'p' && c != 'x' && c != 'f') {
                nonZero = true;
                break;
            }
            if (c == 'e' || c == 'p') { break; }
        }
        if (nonZero && Float.parseFloat($fd.text) == 0) {
            System.err.println(tmploc.getFilename() + ":" + tmploc.getLine()
                + ":" + tmploc.getPositionInLine() + ": float literal underflow.");
            throw new NumberFormatException();
        } else {
            try {
                $tree = new FloatLiteral(Float.parseFloat($fd.text));
            } catch (NumberFormatException e) {
                System.err.println(tmploc.getFilename() + ":" + tmploc.getLine()
                    + ":" + tmploc.getPositionInLine() + ": float literal out of expected range.");
                    throw e;
                }
        }
        LOG.trace($tree);
        }
    | STRING {
        $tree = new StringLiteral($STRING.text);
        LOG.trace($tree);
    }
    | TRUE {
        $tree = new BooleanLiteral(Boolean.valueOf($TRUE.text));
        LOG.trace($tree);
        }
    | FALSE {
        $tree = new BooleanLiteral(Boolean.valueOf($FALSE.text));
        LOG.trace($tree);
        }
    | THIS {
        $tree = new This(false);
        LOG.trace($tree);
        }
    | NULL {
        $tree = new Null();
        LOG.trace($tree);
        }
    ;

ident returns[AbstractIdentifier tree]
    : IDENT {
            $tree = new Identifier(this.getDecacCompiler().getSymbTable().create($IDENT.text));
            setLocation($tree, $start);
            LOG.trace($tree);
        }
    ;

/****     Class related rules     ****/

list_classes returns[ListDeclClass tree]
@init   {
            $tree = new ListDeclClass();
        }
    :
      (c1=class_decl {
            $tree.add($c1.tree);
        }
      )*
    ;

// Class-related
class_decl returns[AbstractDeclClass tree]
    : CLASS name=ident superclass=class_extension OBRACE class_body CBRACE {
            $tree = new DeclClass($name.tree, $superclass.tree, $class_body.flds, $class_body.meths);
            setLocation($tree, $start);
        }
    ;

// Class-related
class_extension returns[AbstractIdentifier tree]
    : EXTENDS ident {
            $tree = $ident.tree;
            LOG.trace($tree);
        }
    | /* epsilon */ {
            Identifier superclass = new Identifier(this.getDecacCompiler().getSymbTable().create("Object"));
            $tree = superclass;
            setLocation($tree, $start);
            LOG.trace($tree);
        }
    ;

// Class-related
class_body returns[ListDeclField flds, ListDeclMethod meths]
@init   {
            $meths = new ListDeclMethod();
            $flds = new ListDeclField();
        }
    : (m=decl_method {
            assert($m.tree != null);
            $meths.add($m.tree);
        }
      | f=decl_field_set {
            assert($f.tree != null);
            $flds.addAll($f.tree);
        }
      )*
    ;

// Class-related
decl_field_set returns [ListDeclField tree]
@init   {
            $tree = new ListDeclField();
        }
    : vis=visibility t=type list_decl_field[$tree,$vis.v,$t.tree]
      SEMI
    ;

// Class-related
visibility returns [Visibility v]
    : /* epsilon */ {
        $v = Visibility.PUBLIC;
        }
    | PROTECTED {
        $v = Visibility.PROTECTED;
        }
    ;

// Class-related
list_decl_field[ListDeclField tree, Visibility v, AbstractIdentifier t]
    : dv1=decl_field[$v, $t] {
            $tree.add($dv1.tree);
    }
        (COMMA dv2=decl_field[$v, $t] {
            $tree.add($dv2.tree);
        }
      )*
    ;

// Class-related
decl_field[Visibility v, AbstractIdentifier t] returns [AbstractDeclField tree]
@init   {
            $tree = null;
        }
    : i=ident {
            setLocation($i.tree,$i.start);
        }
      (eq=EQUALS e=expr {
            $tree = new DeclField($v, $t, $i.tree, new Initialization($e.tree));
            setLocation($tree.getInit(), $eq);
            setLocation($tree, $i.start);
            LOG.trace($tree);
        }
      )? {
            if ($tree == null) {
                $tree = new DeclField($v, $t, $i.tree, new NoInitialization());
                setLocation($tree, $i.start);
                LOG.trace($tree);
            }
        }
    ;

// Class-related
decl_method returns [AbstractDeclMethod tree]
    : type ident OPARENT params=list_params CPARENT (block {
            MethodBody mBody = new MethodBody($block.decls, $block.insts);
            setLocation(mBody, $block.start);
            $tree = new DeclMethod($type.tree, $ident.tree, $params.tree, mBody);
            LOG.trace($tree);
        }
      | as=ASM OPARENT code=multi_line_string CPARENT SEMI {
            StringLiteral s = new StringLiteral($code.text);
            setLocation(s, $code.start);
            MethodAsmBody asmBody = new MethodAsmBody(s);
            setLocation(asmBody, $as);
            $tree = new DeclMethod($type.tree, $ident.tree, $params.tree, asmBody);
            LOG.trace($tree);
        }
      ) {
            setLocation($tree, $type.start);
        }
    ;

// Class-related
list_params returns [ListDeclParam tree]
@init   {
            $tree = new ListDeclParam();
        }
    : (p1=param {
            $tree.add($p1.tree);
        } (COMMA p2=param {
            $tree.add($p2.tree);
        }
      )*)?
    ;
    
multi_line_string returns[String text, Location location]
    : s=STRING {
            $text = $s.text;
            $location = tokenLocation($s);
        }
    | s=MULTI_LINE_STRING {
            $text = $s.text;
            $location = tokenLocation($s);
        }
    ;

param returns [AbstractDeclParam tree]
    : type ident {
            $tree = new DeclParam($type.tree, $ident.tree);
            setLocation($tree, $ident.start);
        }
    ;
